&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.КартинкаГруппы.Получить() <> Неопределено Тогда
		КартинкаФона = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "КартинкаГруппы");   
		Элементы.УдалитьКартинку.Видимость = Истина;
	Иначе
		Элементы.УдалитьКартинку.Видимость = Ложь;
	КонецЕсли;
	
	ЦветФона = Объект.ЦветФона.Получить();
	Элементы.ОтображениеВРабочемМестеКассира.Видимость = Объект.ОтображатьВРабочемМестеКассира;

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьВРабочемМестеКассираПриИзменении(Элемент)

	Элементы.ОтображениеВРабочемМестеКассира.Видимость = Объект.ОтображатьВРабочемМестеКассира;

КонецПроцедуры


&НаКлиенте
Процедура ЦветФонаПриИзменении(Элемент)

	ЦветФонаПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ЦветФонаПриИзмененииНаСервере()

	Объект.ЦветФона = Новый ХранилищеЗначения(ЦветФона, Новый СжатиеДанных());
	Модифицированность = Истина;

КонецПроцедуры


&НаКлиенте
Асинх Процедура ЗагрузитьКартинку(Команда)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов; 
	
	Файл = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	
	Если Файл <> Неопределено Тогда
		КартинкаФона = Файл.Адрес;
		Модифицированность = Истина;  
		КартинкаЗагружена = Истина; 
		КартинкаУдалена = Ложь;    
		Элементы.УдалитьКартинку.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)  
	
	Если КартинкаЗагружена Тогда
		ТекущийОбъект.КартинкаГруппы = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(КартинкаФона));
		УдалитьИзВременногоХранилища(КартинкаФона);
	КонецЕсли;
	
	Если КартинкаУдалена Тогда
		ТекущийОбъект.КартинкаГруппы = Неопределено;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура УдалитьКартинку(Команда)      

	Модифицированность = Истина;  
	КартинкаФона = "";
	КартинкаУдалена = Истина;      
	КартинкаЗагружена = Ложь;  
	Элементы.УдалитьКартинку.Видимость = Ложь;

КонецПроцедуры

