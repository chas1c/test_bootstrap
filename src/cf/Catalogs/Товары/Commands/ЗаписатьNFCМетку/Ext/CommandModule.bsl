
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

#Если МобильныйКлиент Тогда 
	МеткиNDEF = СредстваNFC.МеткиNDEF;
	Если МеткиNDEF.ПоддерживаетсяЗаписьНаМетку() Тогда   
		ОповещениеОПоявленииМетки = Новый ОписаниеОповещения("ПриПоявленииМеткиЗаписатьСообщениеNDEF", ЭтотОбъект, ПараметрКоманды);
		МеткиNDEF.ВключитьАктивноеСканирование(, ОповещениеОПоявленииМетки);
	Иначе
		Сообщить(НСтр("ru = 'Нет возможности для записи метки'", "ru"));
	КонецЕсли;
#Иначе
	Сообщить(НСтр("ru = 'Доступно только в мобильном клиенте'", "ru"));
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриПоявленииМеткиЗаписатьСообщениеNDEF(Метка, ДополнительныеДанные) Экспорт
	
#Если МобильныйКлиент Тогда 
	Если Метка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка 
		Записи = Новый Массив(1);
		Записи[0] = Новый ТекстоваяЗаписьNDEF(Строка(ДополнительныеДанные.УникальныйИдентификатор()));
		ФиксированныеЗаписи = Новый ФиксированныйМассив(записи);
		Сообщение = Новый СообщениеNDEF(ФиксированныеЗаписи);
		
		Ждать Метка.ЗаписатьСообщениеАсинх(Сообщение);
	Исключение
		Сообщить(НСтр("ru = 'Произошла ошибка, попробуйте еще раз.'", "ru"));
	Конецпопытки;
#КонецЕсли
	
КонецПроцедуры
