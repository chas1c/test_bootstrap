&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если (МобильноеПриложениеКлиент Или МобильныйКлиент) Тогда
		Элементы.ПродажиВсего.РастягиватьПоВертикали = Ложь;
		Элементы.ДиаграммаПродажиПоНоменклатуре.РастягиватьПоВертикали = Ложь;
		Элементы.ДиаграммаФинансовыеДокументы.РастягиватьПоВертикали = Ложь;
		Элементы.ДиаграммаЗаказыТоваров.РастягиватьПоВертикали = Ложь;
	#КонецЕсли
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	ПодготовитьЦвета();
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	СхемаКомпоновки =  ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
		
    КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновки,
								Отчет.КомпоновщикНастроек.ПолучитьНастройки());
    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,, Истина);
    ПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	РезультатОтчета = Новый ТабличныйДокумент;  
	
    ПроцессорВыводаРезультата.УстановитьДокумент(РезультатОтчета);
    ПроцессорВыводаРезультата.Вывести(ПроцессорКомпоновкиДанных);
		
	ЗаполнитьДиаграммы(РезультатОтчета);
	
КонецПроцедуры	

&НаСервере
Процедура ПодготовитьЦвета()
	
	//Для таблицы
	ОформлениеЗначенийТаблицы = ХранилищеСистемныхНастроек.Загрузить("Общее/ЦветаТаблицы/Значки");
	Если ОформлениеЗначенийТаблицы = Неопределено Тогда
		ОформлениеЗначенийТаблицы = Новый ОформлениеЗначений;
	КонецЕсли;
	
	ОформлениеЗначенийТаблицы.Установить(Перечисления.СостоянияЗаказов.Открыт, 2);
	ОформлениеЗначенийТаблицы.Установить(Перечисления.СостоянияЗаказов.ВРаботе, 1);
	ОформлениеЗначенийТаблицы.Установить(Перечисления.СостоянияЗаказов.Выполнен, 0);
	ОформлениеЗначенийТаблицы.Установить(Перечисления.СостоянияЗаказов.Закрыт, 3);
	
	ХранилищеСистемныхНастроек.Сохранить("Общее/ЦветаТаблицы/Значки", ,ОформлениеЗначенийТаблицы);
	
	//Для диаграммы
	ОформлениеЗначенийДиаграммы = ХранилищеСистемныхНастроек.Загрузить("Общее/ЦветаДиаграммы");
	Если ОформлениеЗначенийДиаграммы = Неопределено Тогда
		ОформлениеЗначенийДиаграммы = Новый ОформлениеЗначений;
	КонецЕсли;
			
	ОформлениеЗначенийДиаграммы.Установить(ТипЗнч(Документы.ПоступлениеДенег.ПустаяСсылка()), ЦветаПалитры.Розовый);
	ОформлениеЗначенийДиаграммы.Установить(ТипЗнч(Документы.Оплата.ПустаяСсылка()), ЦветаПалитры.Сиреневый);
	
	ХранилищеСистемныхНастроек.Сохранить("Общее/ЦветаДиаграммы", ,ОформлениеЗначенийДиаграммы); 
		
КонецПроцедуры

&НаСервере
Процедура ПереместитьСерииДиаграммыВТочки(Диаграмма)
	ЦветаСерий = Новый Массив;
	Для Индекс = 0 по Диаграмма.КоличествоСерий - 1 Цикл 
		ЦветаСерий.Добавить(Диаграмма.Серии[Индекс].Цвет);
	КонецЦикла;
	
	Диаграмма.Транспонировать();
	
	Для Индекс = 0 по Диаграмма.КоличествоТочек - 1 Цикл 
		Диаграмма.Точки[Индекс].Цвет = ЦветаСерий[Индекс];
		Диаграмма.Точки[Индекс].ПриоритетЦвета = Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммы(РезультатОтчета)
	
	ДиаграммаПродажиВсего = РезультатОтчета.Рисунки[0].Объект;
	
	ДиаграммаПродажиПоНоменклатуре = РезультатОтчета.Рисунки[1].Объект;
	ПереместитьСерииДиаграммыВТочки(ДиаграммаПродажиПоНоменклатуре);
	ДиаграммаПродажиПоНоменклатуре.Серии[0].Текст = "Количество";
	ДиаграммаПродажиПоНоменклатуре.КоличествоТочек = Мин(ДиаграммаПродажиПоНоменклатуре.КоличествоТочек, 6);
	
	
	ДиаграммаФинансовыеДокументы = РезультатОтчета.Рисунки[2].Объект;
	ПереместитьСерииДиаграммыВТочки(ДиаграммаФинансовыеДокументы);
		
	ДиаграммаЗаказыТоваров = РезультатОтчета.Рисунки[3].Объект;
		
	//Синхронизируем цвета таблицы и диаграммы заказов
	ПалитраТаблицы = Элементы.ЗаказыТоваров.ОписаниеПалитрыЦветовЗначков.ПолучитьПалитру();
	ДиаграммаЗаказыТоваров.ОписаниеПалитрыЦветов.УстановитьПалитру(ПалитраТаблицы);
	ОформлениеЗначений = ХранилищеСистемныхНастроек.Загрузить("Общее/ЦветаТаблицы/Значки");
	ДиаграммаЗаказыТоваров.ЗаполнитьЦветаСерий(ОформлениеЗначений);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПродажиВсего(Команда)	

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ДиаграммаПоПериодам"); 
	
	ОткрытьФорму("Отчет.ДинамикаПродаж.Форма", ПараметрыФормы, ЭтаФорма, "ДиаграммаПоПериодам");
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПродажиПоНоменклатуре(Команда)
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ПродажиПоНоменклатуре"); 
	
	ОткрытьФорму("Отчет.ДинамикаПродаж.Форма", ПараметрыФормы, ЭтаФорма, "ПродажиПоНоменклатуре");
КонецПроцедуры

