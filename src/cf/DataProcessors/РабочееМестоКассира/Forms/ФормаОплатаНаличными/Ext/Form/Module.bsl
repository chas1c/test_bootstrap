
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СуммаКОплате = Параметры.СуммаКОплате;
	ПозицииЗаказов.Загрузить(Параметры.ПозицииЗаказов.Выгрузить());
	Валюта = Параметры.Валюта;
	ВалютаНаименование = Валюта.Наименование;
	ВводЦелойЧасти = Истина;
	
	ФорматироватьПолеВвода(Элементы.СуммаКОплате);
	ФорматироватьПолеВвода(Элементы.ПринятоОтКлиента);
	ФорматироватьПолеВвода(Элементы.Сдача);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту50);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту100);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту200);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту500);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту1000);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту2000);
	ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Элементы.ВвестиБанкноту5000);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаголовокКнопкиВвестиБанкноту(Кнопка)

	Кнопка.Заголовок = ФорматироватьЦену(Число(СтрЗаменить(Кнопка.Имя, "ВвестиБанкноту", "")));

КонецПроцедуры

&НаСервере
Функция ФорматироватьЦену(Цена)

	Возврат Формат(Цена, "NFD=2") + Символы.НПП + ВалютаНаименование;

КонецФункции

&НаСервере
Процедура ФорматироватьПолеВвода(ПолеВвода)

	ПолеВвода.ФорматРедактирования = "ЧДЦ=2; ЧФ='Ч" + Символы.НПП + ВалютаНаименование + "'";

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура ПринятоОтКлиентаПриИзменении(Элемент)

	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура ПринятоОтКлиентаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОбновитьСуммуСдачи(Число(СтрЗаменить(Текст, Символы.НПП + ВалютаНаименование, "")));

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСуммуСдачи(ПринятоОтКлиента)

	Сдача = ПринятоОтКлиента - СуммаКОплате;

КонецПроцедуры

&НаКлиенте
Процедура БезСдачи(Команда)
	
	ВводЦелойЧасти = Истина;
	ПринятоОтКлиента = СуммаКОплате;
	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура ВвестиБанкноту(Команда)

	ВводЦелойЧасти = Истина;
	ПринятоОтКлиента = ПринятоОтКлиента + Число(СтрЗаменить(ТекущийЭлемент.Имя, "ВвестиБанкноту", ""));
	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)

	ВводЦелойЧасти = Истина;
	ПринятоОтКлиента = 0;
	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура ВвестиЦифру(Команда)

	Цифра = Число(СтрЗаменить(ТекущийЭлемент.Имя, "ВвестиЦифру", ""));
	ЦелаяЧасть = Цел(ПринятоОтКлиента);
	ДробнаяЧасть = ПринятоОтКлиента - ЦелаяЧасть;
	Если ВводЦелойЧасти Тогда
		ЦелаяЧасть = 10 * ЦелаяЧасть + Цифра;
	Иначе
		ДробнаяЧасть = 0.1 * (Цифра + ДробнаяЧасть);
	КонецЕсли;
	ПринятоОтКлиента = ЦелаяЧасть + Цел(ДробнаяЧасть * 100) / 100;
	ОбновитьСуммуСдачи(ПринятоОтКлиента);

КонецПроцедуры

&НаКлиенте
Процедура ВвестиРазделитель(Команда)

	ВводЦелойЧасти = Не ВводЦелойЧасти;

КонецПроцедуры

&НаКлиенте
Асинх Процедура Оплатить(Команда)
	
	СоздатьПродажу();

	Если Сдача > 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Не забудьте отдать клиенту сдачу " + ФорматироватьЦену(Сдача) + " и кассовый чек.'", "ru");
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не забудьте отдать клиенту кассовый чек.'", "ru");
	КонецЕсли;
	Ждать ПредупреждениеАсинх(ТекстПредупреждения,, НСтр("ru = 'Заказ оплачен'", "ru"));
   
   	Закрыть(Истина);

КонецПроцедуры

&НаСервере
Процедура СоздатьПродажу()

	РасходТовара = Документы.РасходТовара.СоздатьДокумент();
	РасходТовара.Дата = ТекущаяДата();
	РасходТовара.Валюта = Валюта;
	РасходТовара.Покупатель = Справочники.Контрагенты.НеизвестныйПокупатель;
	РасходТовара.ВидЦен = Справочники.ВидыЦен.Розничная;
	РасходТовара.Товары.Загрузить(ПозицииЗаказов.Выгрузить());
	РасходТовара.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);

КонецПроцедуры


