

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	КодНачальнойКорневойГруппыТоваров = ХранилищеОбщихНастроек.Загрузить("КодНачальнойКорневойГруппыТоваровРабочегоМестаКассира");
	Если КодНачальнойКорневойГруппыТоваров <> Неопределено Тогда
		НачальнаяКорневаяГруппаТоваров = Справочники.Товары.НайтиПоКоду(КодНачальнойКорневойГруппыТоваров);
	КонецЕсли;
	
	КодВалюты = ХранилищеОбщихНастроек.Загрузить("КодВалютыРабочегоМестаКассира");
	Если КодВалюты <> Неопределено Тогда
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
	КонецЕсли;

	Элементы.ФормаСохранитьИПерезапустить.Доступность = Модифицированность;

КонецПроцедуры

&НаКлиенте
Процедура НачальнаяКорневаяГруппаТоваровПриИзменении(Элемент)

	Модифицированность = Истина;
	Элементы.ФормаСохранитьИПерезапустить.Доступность = Модифицированность;

КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)

	Модифицированность = Истина;
	Элементы.ФормаСохранитьИПерезапустить.Доступность = Модифицированность;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьИПерезапустить(Команда)

	СохранитьНаСервере();
	Модифицированность = Ложь;
	ЗавершитьРаботуСистемы(,Истина);

КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()

	ХранилищеОбщихНастроек.Сохранить("КодНачальнойКорневойГруппыТоваровРабочегоМестаКассира",, НачальнаяКорневаяГруппаТоваров.Код);
	ХранилищеОбщихНастроек.Сохранить("КодВалютыРабочегоМестаКассира",, Валюта.Код);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Модифицированность Тогда
		Отказ = Истина;
		Если НЕ ЗавершениеРаботы Тогда
			СтандартнаяОбработка = Ложь;
			ДействияПриМодифицирпрванностиНастроек();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура ДействияПриМодифицирпрванностиНастроек()

	Текст = НСтр("ru='Настройки были изменены. Сохранить изменения?'", "ru");
	Режим = РежимДиалогаВопрос.ДаНетОтмена;
	РезультатВопросаОСохраненииИзменений = Ждать ВопросАсинх(Текст, Режим, 0);
	Если РезультатВопросаОСохраненииИзменений = КодВозвратаДиалога.Да Тогда
		СохранитьНаСервере();
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли РезультатВопросаОСохраненииИзменений = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры
