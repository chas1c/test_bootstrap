
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Макет ТиповыеПриемкиРазработки должен идти последним в списке
	ПозицияМакетаТПР = Неопределено;
	ИмяМакетаТПР = "ТиповыеПриемыРазработки";                      

	//Макет ДополнительныеДемонстрационныеРесурсы должен идти после макета ДемонстрационнаяКонфигурация
	ПозицияМакетаДДР = Неопределено;
	ПозицияДК = Неопределено;
		
	ИмяМакетаДК = "ДемонстрационнаяКонфигурация";
	ИмяМакетаДДР = "ДополнительныеДемонстрационныеРесурсы";

	Для Сч = 0 По Метаданные.Обработки.Путеводитель.Макеты.Количество() - 1 Цикл
		МетД = Метаданные.Обработки.Путеводитель.Макеты.Получить(Сч);
		
		//Запоминаем позицию макетов, для которых нужна вставка в определенное место
		Если МетД.Имя = ИмяМакетаТПР Тогда
			ПозицияМакетаТПР = Сч;
			Продолжить;
		КонецЕсли;
		Если МетД.Имя = ИмяМакетаДДР Тогда
			ПозицияМакетаДДР = Сч;
			Продолжить;
		КонецЕсли;
		
		//Вносим информацию по макетам, позиция вставки для которых не важна
		Стр = РазделыКонфигурации.Добавить();
		Стр.Название = МетД.Имя;
		Стр.Описание = МетД.Синоним; 

		Если МетД.Имя = ИмяМакетаДК Тогда
			ПозицияДК = РазделыКонфигурации.Количество();
		КонецЕсли;
	КонецЦикла;

	//Добавляем макет ДополнительныеДемонстрационныеРесурсы после макета ДемонстрационнаяКонфигурация
	Если  ПозицияМакетаДДР <> Неопределено И ПозицияДК <> Неопределено Тогда
		МетД = Метаданные.Обработки.Путеводитель.Макеты.Получить(ПозицияМакетаДДР);
		Стр = РазделыКонфигурации.Вставить(ПозицияДК);
		Стр.Название = МетД.Имя;
		Стр.Описание = МетД.Синоним;
	КонецЕсли;

	//Добавляем последним макет ТиповыеПриемкиРазработки
	Если  ПозицияМакетаТПР <> Неопределено Тогда
		МетД = Метаданные.Обработки.Путеводитель.Макеты.Получить(ПозицияМакетаТПР);
		Стр = РазделыКонфигурации.Добавить();
		Стр.Название = МетД.Имя;
		Стр.Описание = МетД.Синоним;
	КонецЕсли;
		
	СформироватьПутеводитель();
	
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Если ТекущийПользователь.Роли.Содержит(Метаданные.Роли.Администратор) Тогда
		Элементы.Предупреждение.Видимость = Ложь;
	Иначе
		Элементы.Предупреждение.Видимость = Истина;
		ЭтаФорма.Высота = ЭтаФорма.Высота + 2;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИндексТекущейСтраницы = Элементы.Страницы.ПодчиненныеЭлементы.Индекс(Элементы.Страницы.ТекущаяСтраница);
	Элементы.ЗаголовкиРазделов.ТекущаяСтрока = ИндексТекущейСтраницы;
	АктуализироватьДоступностьКнопокНазадВперед(Элементы.Страницы.ТекущаяСтраница);
	#Если (МобильноеПриложениеКлиент Или МобильныйКлиент) Тогда
		Для Сч = 0 По РазделыКонфигурации.Количество() - 1 Цикл
			Раздел = РазделыКонфигурации.Получить(Сч);
			ПолеHTMLДокумента = Элементы.Найти(Раздел.Название + "ПолеHTMLДокумента");
			ПолеHTMLДокумента.РастягиватьПоВертикали = Ложь;
		КонецЦикла;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ОтбработкаВыбораОбластиПутеводителя(ДанныеСобытия.Href, ДанныеСобытия.Anchor);
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	ИндексТекущейСтраницы = Элементы.Страницы.ПодчиненныеЭлементы.Индекс(Элементы.Страницы.ТекущаяСтраница);
	НовыйИндексТекущейСтраницы = Макс(ИндексТекущейСтраницы - 1, 0);
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Получить(НовыйИндексТекущейСтраницы);
	Элементы.ЗаголовкиРазделов.ТекущаяСтрока = НовыйИндексТекущейСтраницы;
	АктуализироватьДоступностьКнопокНазадВперед(Элементы.Страницы.ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	ИндексТекущейСтраницы = Элементы.Страницы.ПодчиненныеЭлементы.Индекс(Элементы.Страницы.ТекущаяСтраница);
	НовыйИндексТекущейСтраницы = Мин(ИндексТекущейСтраницы + 1, Элементы.Страницы.ПодчиненныеЭлементы.Количество() - 1);
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Получить(НовыйИндексТекущейСтраницы);
	Элементы.ЗаголовкиРазделов.ТекущаяСтрока = НовыйИндексТекущейСтраницы;
	АктуализироватьДоступностьКнопокНазадВперед(Элементы.Страницы.ТекущаяСтраница);
КонецПроцедуры

&НаСервере
Функция СформироватьПутеводитель()
	ДобавляемыеРеквизиты = Новый Массив();
	Для Сч = 0 По РазделыКонфигурации.Количество() - 1 Цикл
		Раздел = РазделыКонфигурации.Получить(Сч);
		Реквизит = Новый РеквизитФормы(Раздел.Название, Новый ОписаниеТипов("Строка"),,, Ложь);
		ДобавляемыеРеквизиты.Добавить(Реквизит);
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	Для Сч = 0 По РазделыКонфигурации.Количество() - 1 Цикл
		Раздел = РазделыКонфигурации.Получить(Сч);
		
		ЗаголовкиРазделов.Добавить(Раздел.Название, Раздел.Описание);
		
		Страница = Элементы.Добавить(Раздел.Название + "Страница", Тип("ГруппаФормы"), Элементы.Страницы);
        Страница.Вид = ВидГруппыФормы.Страница;
		Страница.Заголовок = Раздел.Описание;
		
		ПолеHTMLДокумента = Элементы.Добавить(Раздел.Название + "ПолеHTMLДокумента", Тип("ПолеФормы"), Страница);
		ПолеHTMLДокумента.Вид = ВидПоляФормы.ПолеHTMLДокумента;
		ПолеHTMLДокумента.ПутьКДанным = Раздел.Название;
		ПолеHTMLДокумента.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеHTMLДокумента.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
		ПолеHTMLДокумента.УстановитьДействие("ПриНажатии", "ПолеHTMLПриНажатии");

		ДокументHTML = Обработки.Путеводитель.ПолучитьМакет(Раздел.Название).ПолучитьДокументHTML();
		ЗапиcьHTML = Новый ЗаписьHTML;
		ЗапиcьHTML.УстановитьСтроку();
		ЗаписьDOM = Новый ЗаписьDOM;
		ЗаписьDOM.Записать(ДокументHTML, ЗапиcьHTML);
		ЭтотОбъект[Раздел.Название] = ЗапиcьHTML.Закрыть();
	КонецЦикла;
КонецФункции

&НаКлиенте
Асинх Процедура ОтбработкаВыбораОбластиПутеводителя(Ссылка, Якорь)
#Если МобильныйКлиент Тогда
	Позиция = Найти(Ссылка, "ОткрытьФорму=");
	Если Позиция > 0 Тогда
		ОткрытьФорму(Сред(Ссылка, Позиция + 13));
	КонецЕсли;
	Позиция = Найти(Ссылка, "ОткрытьСправку=");
	Если Позиция > 0 Тогда
		ОткрытьСправку(Сред(Ссылка, Позиция + 15));
	КонецЕсли;
	Позиция = Найти(Ссылка, "ОткрытьСсылку=");
	Если Позиция > 0 Тогда
		ПерейтиПоНавигационнойСсылке(Сред(Ссылка, Позиция + 14));
	КонецЕсли;
#Иначе
	Если Якорь <> Неопределено Тогда
		Если Якорь.protocol = "v8:" Тогда
			Позиция = Найти(Ссылка, "ОткрытьФорму=");
			Если Позиция > 0 Тогда
				ОткрытьФорму(Сред(Ссылка, Позиция + 13));
			КонецЕсли;
			Позиция = Найти(Ссылка, "ОткрытьСправку=");
			Если Позиция > 0 Тогда
				ОткрытьСправку(Сред(Ссылка, Позиция + 15));
			КонецЕсли;
			Позиция = Найти(Ссылка, "ОткрытьСсылку=");
			Если Позиция > 0 Тогда
				ПерейтиПоНавигационнойСсылке(Сред(Ссылка, Позиция + 14));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьДоступностьКнопокНазадВперед(ТекущаяСтраница);
	ИндексТекущейСтраницы = Элементы.Страницы.ПодчиненныеЭлементы.Индекс(ТекущаяСтраница);
	Элементы.ФормаНазад.Доступность = ИндексТекущейСтраницы <> 0;
	Элементы.ФормаВперед.Доступность = ИндексТекущейСтраницы <> Элементы.Страницы.ПодчиненныеЭлементы.Количество() - 1;
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовкиРазделовПриАктивизацииСтроки(Элемент)
	ИндексТекущейСтроки = Элементы.ЗаголовкиРазделов.ТекущаяСтрока;
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Получить(ИндексТекущейСтроки);
	АктуализироватьДоступностьКнопокНазадВперед(Элементы.Страницы.ТекущаяСтраница);
КонецПроцедуры

